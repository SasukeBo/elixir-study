# ETS

在前面的学习中，每次获取bucket都要向registry发送一个信息，因此，当很多进程同时

通过registry获取bucket时，registry就会成为一个瓶颈。

在这一章的学习中，我们将学习ETS，如何使用它的缓存机制。

## ETS as a cache

ETS允许我们在内存中存储任何Elixir数据类型。底层是通过Erlang的`:ets`模块实现的。

```sh
iex> table = :ets.new(:buckets_registry, [:set, :protected])
#Reference<0.1885502827.460455937.234656>
iex> :ets.insert(table, {"foo", self()})
true
iex> :ets.lookup(table, "foo")
[{"foo", #PID<0.41.0>}]
```
创建一个ETS table需要两个参数，table的名字和一些设置选项。上面的例子，我们设置了

table type和访问规则（:set type和:protected访问规则）。`:set` type意味着键值对的键

（key）是不可重复的，`:protected` 表示只有创建这个table的进程可以进行写操作，但是

所有进程都可以读取这个table。实际上两个选项的默认设置就是如此。


ETS tables 可以被命名，通过name可以直接访问这些table：

```sh
iex> :ets.new(:buckets_registry, [:named_table])
:buckets_registry
iex> :ets.insert(:buckets_registry, {"foo", self()})
true
iex> :ets.lookup(:buckets_registry, "foo")
[{"foo", #PID<0.41.0>}]
```

现在试着在`KV.Registry`中使用ETS tables。首先需要修改的是让registry可以接收name参

数，这个name参数用来给table命名。因为ETS tables的名字和进程命名存储是分开的，所

以不会发生命名冲突。

`registry.ex`修改如下：

```elixir
defmodule KV.Registry do
  use GenServer

  ## Client API

  @doc """
  Starts the registry.
  """
  def start_link(opts) do
    # 1. Pass the name to GenServer's init
    server = Keyword.fetch!(opts, :name)
    GenServer.start_link(__MODULE__, server, opts)
  end

  @doc """
  Looks up the bucket pid for `name` stored in `server`.

  Returns `{:ok, pid}` if the bucket exists, `error` otherwise.
  """
  def lookup(server, name) do
    # 2. Lookup is now done directly in ETS, without accessing the server
    case :ets.lookup(server, name) do
      [{^name, pid}] -> {:ok, pid}
      [] -> :error
    end
  end

  @doc """
  Ensures there is a bucket associated with the given `name` in `server`.
  """
  def create(server, name) do
    GenServer.cast(server, {:create, name})
  end

  ## Server Callbacks

  def init(table) do
    # 3. We have replaced the names map by the ETS table
    names = :ets.new(table, [:named_table, read_concurrency: true])
    refs = %{}
    {:ok, {names, refs}}
  end

  # 4. The previous handle_call callback for lookup was removed

  def handle_cast({:create, name}, {names, refs}) do
    # 5. Read and write to the ETS table instead of the map
    case lookup(names, name) do
      {:ok, _pid} ->
        {:noreply, {names, refs}}

      :error ->
        {:ok, pid} = DynamicSupervisor.start_child(KV.BucketSupervisor, KV.Bucket)
        ref = Process.monitor(pid)
        refs = Map.put(refs, ref, name)
        :ets.insert(names, {name, pid})
        {:noreply, {names, refs}}
    end
  end

  @doc """
  这个回调函数会处理所有发送给Registry进程的信息，例如:

  ## Example

      iex> {:ok, pid} = KV.Registry.start_link([])
      iex> send(pid, {:DOWN, "123", :process, "", ""})
      "123"

  如果在里面打印ref，会发现就是"123"
  """
  def handle_info({:DOWN, ref, :process, _pid, _reason}, {names, refs}) do
  # 6. Delete from the ETS table instead of the map
    {name, refs} = Map.pop(refs, ref)
    :ets.delete(names, name)
    {:noreply, {names, refs}}
  end

  def handle_info(_msg, state) do
    {:noreply, state}
  end
end
```

修改之前我们查询bucket需要向服务器发送请求，服务器查询到bucket返回进程id，而现在

查询bucket则不需要registry，直接在ETS table中查询获取bucket，并且这个table是进程

共享的。这是缓存机制的主要思想。

`read_concurrency: true`的作用是优化并发读操作。

接着修改一下测试代码中的setup：

```elixir
  setup context do
    _ = start_supervised!({KV.Registry, name: context.test})
    %{registry: context.test}
  end
```
由于每个测试的名字都不一样，可以用测试的名字来命名registry，这样就不需要pid来访问

registry。当我们修改setup之后，测试还是会失败，例如"spawns buckets"测试：

```elixir
  test "spawns buckets", %{registry: registry} do
    assert KV.Registry.lookup(registry, "shopping") == :error

    KV.Resgistry.create(registry, "shopping")
    assert {:ok, bucket} = KV.Registry.lookup(registry, "shopping")

    KV.Bucket.put(bucket, "milk", 1)
    assert KV.Bucket.get(bucket, "milk") == 1
  end
```
这段测试可能在这一行失败：
```elixir
  {:ok, bucket} = KV.Registry.lookup(registry, "shopping")
```
为什么明明已经create了"shopping" bucket，还是会失败呢？可能的原因为：

* `lookup`的过早（We are prematurely optimizing (by adding this cache layer)）。
* 创建bucket时使用的是`cast/2`，应该使用`call/2`。


## Race condition?

使用Elixir开发并没有避免进程之间的资源竞争。但是Elixir静态语言的特性让底层的资源

竞争更容易被发现。

测试失败的原因是因为创建table有延迟，而我们的create请求不需要得到服务端的请求响应，

所以测试中客户端只是将create请求发给了server，然后就去执行下一条：`lookup`，而此时

server应该还没能完成table的创建，这就造成`lookup`会失败。

我们想要的事件顺序是：

1. 调用`create`。
2. registry创建bucket，插入到ETS table中。
3. `lookup` ETS table中的bucket。
4. 得到`{:ok, bucket}`。

而实际发生的事件顺序是：

1. 调用`create`。
2. `lookup` ETS table中的bucket。
3. 指令返回`:error`
4. registry创建bucket，插入到ETS table中。

要解决这个问题就必须确保`KV.Registry.create/2`是同步执行的，弃用`cast/2`，使用`call/2`，

这样做可以确保客户端等待服务器执行完创建bucket的操作，修改如下：
```elixir
  def create(server, name) do
    GenServer.call(server, {:create, name})
  end

  def handle_call({:create, name}, _from, {names, refs}) do
    case lookup(names, name) do
      {:ok, pid} ->
        {:reply, pid, {names, refs}}

      :error ->
        {:ok, pid} = DynamicSupervisor.start_child(KV.BucketSupervisor, KV.Bucket)
        ref = Process.monitor(pid)
        refs = Map.put(refs, ref, name)
        :ets.insert(names, {name, pid})
        {:reply, pid, {names, refs}}
    end
  end
```
上面到修改将回调函数从`handle_cast/2`改为`handle_call/2`，返回给client的消息中包含了

新建的bucket的pid。所以Elixir开发者应该更趋向于使用`call/2`，因为它有反馈。


再次运行测试，在测试指令后加上`--trace`选项。这个选项在测试发生死锁和资源竞争时非常

有用，它使得测试同步运行，即使设置了`async: true`也不会生效。

多次运行测试后会发现可能有一到两个测试发生错误，例如：
```sh
1) test removes buckets on exit (KV.RegistryTest)
     test/kv/registry_test.exs:19
     Assertion with == failed
     code: KV.Registry.lookup(registry, "shopping") == :error
     lhs:  {:ok, #PID<0.109.0>}
     rhs:  :error
     stacktrace:
       test/kv/registry_test.exs:23
```
失败信息表明，执行删除的bucket依然存留在ETS table中，这个错误发生的原因主要是删除

命令发出后到ETS table中bucket被移除是有时间延迟的。

不幸的是，面对这个问题，不能再简单的修改`handle_info/2`来使它同步执行删除解决问题。

需要找到一个办法，确保registry在收到bucket崩溃消息后及时做处理。

通过发送同步请求给registry可以解决这个问题：首先进程消息是按照顺序被处理的，如果

registry回复请求在`Agent.stop`被调用之后，就能保证registry已经处理了`:DOWN`消息。

来看一段测试：
```elixir
  test "removes buckets on exit", %{registry: registry} do
    KV.Registry.create(registry, "shopping")
    {:ok, bucket} = KV.Registry.lookup(registry, "shopping")
    Agent.stop(bucket)

    # Do a call to ensure the registry processed the DOWN message
    _ = KV.Registry.create(registry, "bogus")
    assert KV.Registry.lookup(registry, "shopping") == :error
  end

  test "removes bucket on crash", %{registry: registry} do
    KV.Registry.create(registry, "shopping")
    {:ok, bucket} = KV.Registry.lookup(registry, "shopping")

    # Stop the bucket with non-normal reason
    Agent.stop(bucket, :shutdown)

    # Do a call to ensure the registry processed the DOWN message
    _ = KV.Registry.create(registry, "bogus")
    assert KV.Registry.lookup(registry, "shopping") == :error
  end
```
每段测试中在`Agent.stop`之后都请求了一次`create`，这样做的目的就是为了确保registry

处理完`handle_info/2`和`handle_call/3`，因为`handle_info/2`不会返回给客户端执行完毕的

反馈，而借助在其之后调用一个`call/2`，而`handle_call/3`一定会返回给客户端执行完毕的

消息，这样就确保在执行`lookup`时，server那边的动作都已经结束了，勉强做到了同步。

现在测试应该能顺利通过了。
